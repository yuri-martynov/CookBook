namespace Dishes

open Types
open System

open Products

open RecipeBuilder

[<AutoOpen>]
module Soups =

    let private мясной_бульон = шаг {
        шаг     "Сварить мясной бульон"
        время   1 час
        работа  2 минуты
        состав  "вода"  2 литра
        состав  "мясо"  0.5 кг
        состав  "соль"  1 столовая_ложка
        состав  "перец чёрный (горошек)" 4 шт
        состав  "луковица" 1 шт    
    }
    
    let private морковь = шаг {
        шаг     "Очистить, нарезать соломкой свёклу"
        работа  3 минуты
        состав  "свёкла"    1 шт    
    }
    
    let private процедить = шаг {
        шаг     "Процедить мясной бульон"
        работа  1 минута
    }
    
    let private добавить_капусту грам = шаг {
        шаг     "Нарезать капусту, добавить в бульон"
        работа  2 минуты
        состав  "капуста"   грам г
    }
    
    let private нарезать_помидоры = шаг {
        шаг     "Очистить от кожуры, нарезать помидоры"
        работа  3 минуты
        состав  "помидор"   2 шт        
    }

    let private борщ = рецепт {
        блюдо   "борщ"
        
        шаг'    мясной_бульон
        шаг'    морковь
        
        шаг     "Очистить, натереть на крупной терке мокровь"
        работа  3 минуты
        состав  "морковь"   1 шт 
    
        шаг'    нарезать_помидоры
        
        шаг     "Потушить нарезанные овощи на растительном масле"  
        время   10 минут
        состав  "растительное масло" 2 столовые_ложки
        
        шаг'    процедить

        шаг     "Очистить и нарезать картофель. Добавить в бульон"
        работа  3 минуты
        время   5 минут
        состав  "картофель" 2 шт
        
        шаг'    (добавить_капусту 200)
        
        шаг     "добавить в бульон тушёные овощи и варить до готовности"
        время   5 минут
        
        шаг     "добавить мелконарезанный чеснок и зелень"
        работа  1 минута
        состав  "зелень"    1 шт
        состав  "чеснок"    2 шт
    }    

    let private щи = рецепт {
        блюдо   "щи из свежей капусты"
        
        шаг'    мясной_бульон
        шаг'    морковь
        
        шаг     "Почистить лук, нарезать"
        работа  3 минуты
        состав  "лук"   1 шт
        
        шаг     "обжарить морковь с луком"
        время   5 минут
        
        шаг'    процедить
        шаг     "добавить поджаренные овощи в бульон"
        шаг'    (добавить_капусту 400)
        
        шаг     "варить"
        время   20 минут

        шаг'    нарезать_помидоры
        
        шаг     "добавить помидоры в бульон и варить"
        время   3 минуты
        
        шаг     "добавить нарезанную зелель"
        состав  "зелень"    1 шт
    }
    
    let soups = [борщ; щи]